import state, graphics, globals, process, clone, utils, text, fs;

struct TerminalResult {
    bool successful;
    str text;

    void constructor(bool successful, str text = "") {
        self.successful = successful;
        self.text = text;
    }
}

struct TerminalCommand {
    str path;

    void constructor(str path = "") {
        self.path = path;
    }

    TerminalResult execute(TerminalInstance instance, Arr<str> args, KeyMap keyMap, State state, GraphicsState graphicsState) {
        FileHandle handle = fs:open(self.path);
        CloneData data = new CloneData();
        data.set<TerminalInstance>("arg0", instance);
        data.set<Arr<str>>("arg1", args);
        data.set<KeyMap>("arg2", keyMap);
        data.set<State>("arg3", state);
        data.set<GraphicsState>("arg4", graphicsState);
        Process proc = new Process(handle.readText(), data);
        instance.process = proc;
        return new TerminalResult(true);
    }
}

struct TerminalTheme {
    str text = "#fff";
    str warn = "#e37900";
    str error = "#bf0000";
}

struct TerminalContext {
    State state;

    Obj<TerminalCommand> commands;

    void constructor(State state, Obj<TerminalCommand> commands = {}) {
        self.state = state;
        self.commands = commands;
    }

    void registerCommand(str id, TerminalCommand command) {
        self!.commands[id] = command;
    }
}

struct TerminalInstance {
    str text = "";
    str currentCmd = "";
    bool hasLine = false;

    Process process;
    TerminalContext context;
    TerminalTheme theme = new TerminalTheme();

    KeyMap map;

    void constructor(TerminalContext context, KeyMap map) {
        self.context = context;
        self.map = map;
    }
    void update() {
        if (self?.process) {
            if(!self!.process.getAlive()) {
                self.process = null;
            } else {
                self.process.continue();
            }
        }
    }

    void clear() {
        self.text = "";
        self.currentCmd = "";
        self.hasLine = false;
    }

    str _destr(str value) {
        if (value[0] == "\"" && value[value.length - 1] == "\"") {
            return value.slice(1,-1);
        }
        return value;
    }
    TerminalResult runCommand(str cmd, State state, GraphicsState graphicsState) {
        Arr<str> tokens = utils:splitChar(cmd, " ", ["intactQuotes"]);
        
        Arr<str> k = self.context.commands.keys();
        if (k.contains(tokens[0])) {
            Arr<str> args = [];
            str key = tokens.shift();
            for (i, tokens.length) {
                elem = tokens[i];
                args.append(self._destr(elem).toString());
            }
            TerminalCommand command = self.context.commands[key];
            return command.execute(self, args, self.map, state, graphicsState);
        }
        
        print("unknown command:", tokens[0], "list:", k);
        return new TerminalResult(false, "unknown command \'" ++ tokens[0] ++ "\'");
    }
}

void init(State state, GraphicsState graphicsState) {
    graphicsState.drawSurface.loadFontFromUrl(state.config.fontUrl);
    globals:setGlobal<TerminalContext>("terminalContext", new TerminalContext(state, {
        "echo": new TerminalCommand("sys/terminal/echo.fbl"),
        "clear": new TerminalCommand("sys/terminal/clear.fbl"),
        
        "state": new TerminalCommand("sys/terminal/state.fbl"),

        "spinny": new TerminalCommand("sys/terminal/spinny.fbl"),
        "fib": new TerminalCommand("sys/terminal/fib.fbl"),
        "fltr": new TerminalCommand("sys/terminal/fltr.fbl"),
    }));

    TerminalContext context = globals:getGlobal<TerminalContext>("terminalContext");
    globals:setGlobal<TerminalInstance>("terminalInstance", new TerminalInstance(context, state.inputState.map));
}

void runCommand(str cmd, TerminalInstance termInstance, State state, GraphicsState graphicsState) {
    TerminalResult result = termInstance.runCommand(cmd, state, graphicsState);

    if (!result.successful) {
        termInstance.text ++= text:embedColor(termInstance.context.state, termInstance.theme.warn);
    }
    termInstance.text ++= result.text;
}
void newLine(TerminalInstance termInstance) {
    if (termInstance.text.length > 0) {
        termInstance.text ++= "\n";
    }
    termInstance.text ++= text:embedReset(termInstance.context.state);
    termInstance.text ++= "> ";
}

void handleInput(State state, GraphicsState graphicsState) {
    TerminalInstance termInstance = globals:getGlobal<TerminalInstance>("terminalInstance");
    
    termInstance.update();

    bool inControl = true;
    if (termInstance?.process) {
        Process proc = termInstance!.process;
        inControl = false;
    }

    if ((!termInstance.hasLine) &&& inControl) {
        newLine(termInstance);
        termInstance.hasLine = true;
    }

    Arr<str> keys = state.inputState.map.keys;
    while (keys.length > 0) {
        str key = keys.pop();
        if (state.inputState.map.downSpecial.contains("Control")) {
            if (key == "c" && termInstance?.process) {
                Process proc = termInstance!.process;
                proc.kill();
            }
        }
        if (inControl) {
            termInstance.text ++= key;
            termInstance.currentCmd ++= key;
        }
    }

    Arr<str> special = state.inputState.map.special;
    while (special.length > 0) {
        str key = special.pop();
        if (inControl) {
            if (key == "Enter" && termInstance.currentCmd.length > 0) {
                termInstance.text ++= "\n";
                runCommand(termInstance.currentCmd, termInstance, state, graphicsState);
                termInstance.currentCmd = "";
                termInstance.hasLine = false;
            }
            if (key == "Backspace" && termInstance.currentCmd.length > 0) {
                termInstance.text = termInstance.text.slice(0,-1);
                termInstance.currentCmd = termInstance.currentCmd.slice(0,-1);
            }
        }
    }
    globals:setGlobal<TerminalInstance>("terminalInstance", termInstance);
}

void drawMain(State state, GraphicsState graphicsState) {
    Surface drawSurf = graphicsState.drawSurface;
    Frame drawFrame = drawSurf.frame;
    TerminalInstance termInstance = globals:getGlobal<TerminalInstance>("terminalInstance");
    
    /* draw background */ {
        drawSurf.setColor("#0e0d10");
        drawSurf.square(drawFrame.getX(), drawFrame.getY(), drawFrame.getW(), drawFrame.getH(), 0);
    }
    /* textbox */ {
        drawSurf.setColor("#fff");
        Arr<num> pos = drawFrame.getTL();
        drawSurf.text(termInstance.text, 10, pos[0] + 5, pos[1] - 17.5);
    }
}

void update(State state, GraphicsState graphicsState) {
    handleInput(state, graphicsState);
    drawMain(state, graphicsState);
}